apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    signingConfigs {
        release{
            storeFile file("C:\\Eigene Dateien\\Documents\\GitHub\\Egg-Manager-Android-App\\KeyStore_v2.jks")
            storePassword "KeyStorePassword"
            keyAlias "key"
            keyPassword "KeyPassword"
        }
    }
    compileSdkVersion 30
    buildFeatures.viewBinding = true
    defaultConfig {
        applicationId "mfdevelopement.eggmanager"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "2.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // export the database schema
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        sourceSets {
            androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            debuggable true
        }
    }
    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // set to true to have all release builds run lint on issues with severity=fatal
        // and abort the build (controlled by abortOnError above) if fatal issues are found
        checkReleaseBuilds true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // whether lint should include full issue explanations in the text error output
        explainIssues true
        // Normally lint will skip generated sources, but you can turn it on with this flag
        checkGeneratedSources true
        // Normally lint will analyze all dependencies along with each module; this ensures
        // that lint can correctly (for example) determine if a resource declared in a library
        // is unused; checking only the library in isolation would not be able to identify this
        // problem. However, this leads to quite a bit of extra computation; a library is
        // analyzed repeatedly, for each module that it is used in.
        checkDependencies true
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'

    def roomVersion = '2.3.0'
    def coreTestingVersion = '2.1.0'
    def materialVersion = '1.5.0-alpha01'
    def navigationVersion = '2.3.5'

    // Layout
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'

    // Room database
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.navigation:navigation-fragment:$navigationVersion"
    implementation "androidx.navigation:navigation-ui:$navigationVersion"
    implementation 'androidx.preference:preference:1.1.1'
    annotationProcessor "androidx.room:room-compiler:$roomVersion"
    androidTestImplementation "androidx.room:room-testing:$roomVersion"

    // Lifecycle components
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"

    // UI
    implementation "com.google.android.material:material:$materialVersion"

    // Graphs
    implementation "com.github.PhilJay:MPAndroidChart:v3.1.0"

    // Tabbed Activities
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // Testing
    androidTestImplementation "androidx.arch.core:core-testing:$coreTestingVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation 'junit:junit:4.13.2'
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.0.1"

    // WorkManager
    def work_version = "2.5.0"
    implementation "androidx.work:work-runtime:$work_version"

}

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}
